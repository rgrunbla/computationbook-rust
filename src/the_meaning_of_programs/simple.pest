alpha = { 'a'..'z' | 'A'..'Z' }
digit = { '0'..'9' }

whitespace = _{ " " }

variable = @ { (alpha) ~ (alpha | digit)* }
number = @ { (digit)+ }

op_add = { "+" }
op_mul = { "*" }
op_lt  = { "<" }

factor = _{ (variable | number) }
term = _{ factor ~ (op_mul ~ factor)* }

expr = { term ~ (op_add ~ term)* }
expr_condition = { term ~ (op_lt ~ term)? }

stat_assign = { variable ~ "=" ~ expr ~ ";" }
stat_while = { "while" ~ "(" ~ expr_condition ~ ")" ~ "{" ~ stats ~ "}" }
stat_if = { ("if" ~ "(" ~ expr_condition ~ ")" ~ "{" ~ stats ~ "}" ~ "else" ~ "{" ~ stats ~ "}" ) |
            ("if" ~ "(" ~ expr_condition ~ ")" ~ "{" ~ stats ~ "}") }

stat = _{ ( stat_if | stat_while | stat_assign ) }

stats = { (stat)* }

simple = _{ soi ~ stats ~ eoi }

